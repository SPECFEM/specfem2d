!========================================================================
!
!                   S P E C F E M 2 D  Version 7 . 0
!                   --------------------------------
!
!     Main historical authors: Dimitri Komatitsch and Jeroen Tromp
!                        Princeton University, USA
!                and CNRS / University of Marseille, France
!                 (there are currently maNZ_IMAGE_color more authors!)
! (c) Princeton University and CNRS / University of Marseille, April 2014
!
! This software is a computer program whose purpose is to solve
! the two-dimensional viscoelastic anisotropic or poroelastic wave equation
! using a spectral-element method (SEM).
!
! This program is free software; you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation; either version 2 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License along
! with this program; if not, write to the Free Software Foundation, Inc.,
! 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
!
! The full text of the license is available in file "LICENSE".
!
!========================================================================


  subroutine write_color_image_snaphot()

#ifdef USE_MPI
  use mpi
#endif

  use constants,only: IMAIN,NGLLX,NGLLZ,REMOVE_PMLS_FROM_JPEG_IMAGES

  use specfem_par,only: myrank,nspec,it,NPROC, &
                        assign_external_model,ibool,kmato,density,rhoext,P_SV, &
                        minus_int_int_pressure_acoustic,minus_int_pressure_acoustic,minus_pressure_acoustic, &
                        minus_int_int_pressure_gravito,minus_int_pressure_gravito,minus_pressure_gravito, &
                        minus_int_int_pressure_gravitoacoustic,minus_int_pressure_gravitoacoustic,minus_pressure_gravitoacoustic, &
                        displ_elastic,veloc_elastic,accel_elastic, &
                        displs_poroelastic,velocs_poroelastic,accels_poroelastic

  ! PML arrays
  use specfem_par,only: PML_BOUNDARY_CONDITIONS,ispec_is_PML

  use specfem_par_movie

  implicit none

  !local variables
  integer :: i,j,k,ispec,iglob,iproc
  integer :: ier
  double precision :: rhol

  if (myrank == 0) then
    write(IMAIN,*)
    write(IMAIN,*) 'Creating color image of size ',NX_IMAGE_color,' x ',NZ_IMAGE_color,' for time step ',it
    call flush_IMAIN()
  endif

  if (imagetype_JPEG >= 1 .and. imagetype_JPEG <= 3) then
    if (myrank == 0) write(IMAIN,*) 'drawing scalar image of part of the displacement vector...'
    call compute_vector_whole_medium(minus_int_int_pressure_acoustic,minus_int_int_pressure_gravitoacoustic, &
                                     minus_int_int_pressure_gravito,displ_elastic,displs_poroelastic)

  else if (imagetype_JPEG >= 4 .and. imagetype_JPEG <= 6) then
    if (myrank == 0) write(IMAIN,*) 'drawing scalar image of part of the velocity vector...'
    call compute_vector_whole_medium(minus_int_pressure_acoustic,minus_int_pressure_gravitoacoustic, &
                                     minus_int_pressure_gravito,veloc_elastic,velocs_poroelastic)

  else if (imagetype_JPEG >= 7 .and. imagetype_JPEG <= 9) then
    if (myrank == 0) write(IMAIN,*) 'drawing scalar image of part of the acceleration vector...'
    call compute_vector_whole_medium(minus_pressure_acoustic,minus_pressure_gravitoacoustic, &
                                     minus_pressure_gravito,accel_elastic,accels_poroelastic)

  else if (imagetype_JPEG >= 11 .and. imagetype_JPEG <= 13) then
    ! allocation for normalized representation in JPEG image
    ! for an atmosphere model
    if (myrank == 0) write(IMAIN,*) 'drawing scalar image of part of normalized displacement vector...'
    call compute_vector_whole_medium(minus_int_int_pressure_acoustic,minus_int_int_pressure_gravitoacoustic, &
                                     minus_int_int_pressure_gravito,displ_elastic,displs_poroelastic)

    do ispec = 1,nspec
      do j = 1,NGLLZ
        do i = 1,NGLLX
          if (assign_external_model) then
            rhol = rhoext(i,j,ispec)
          else
            rhol = density(1,kmato(ispec))
          endif
          iglob = ibool(i,j,ispec)
          vector_field_display(1,iglob) = sqrt(rhol) * vector_field_display(1,iglob)
          vector_field_display(2,iglob) = sqrt(rhol) * vector_field_display(2,iglob)
        enddo
      enddo
    enddo

  else if (imagetype_JPEG >= 14 .and. imagetype_JPEG <= 16) then
    ! allocation for normalized representation in JPEG image
    ! for an atmosphere model
    call compute_vector_whole_medium(minus_int_pressure_acoustic,minus_int_pressure_gravitoacoustic, &
                                     minus_int_pressure_gravito,veloc_elastic,velocs_poroelastic)

    do ispec = 1,nspec
      do j = 1,NGLLZ
        do i = 1,NGLLX
          if (assign_external_model) then
            rhol = rhoext(i,j,ispec)
          else
            rhol = density(1,kmato(ispec))
          endif
          iglob = ibool(i,j,ispec)
          vector_field_display(1,iglob) = sqrt(rhol) * vector_field_display(1,iglob)
          vector_field_display(2,iglob) = sqrt(rhol) * vector_field_display(2,iglob)
        enddo
      enddo
    enddo

  else if (imagetype_JPEG == 10 .and. P_SV) then
    if (myrank == 0) write(IMAIN,*) 'drawing image of pressure field...'
    call compute_pressure_whole_medium()

  else if (imagetype_JPEG == 10 .and. .not. P_SV) then
    call exit_MPI(myrank,'cannot draw pressure field for SH (membrane) waves')

  else
    call exit_MPI(myrank,'wrong type for JPEG snapshots')
  endif

!! DK DK quick hack to remove the PMLs from JPEG images if needed: set the vector field to zero there
  if (PML_BOUNDARY_CONDITIONS .and. REMOVE_PMLS_FROM_JPEG_IMAGES) then
    do ispec = 1,nspec
      if (ispec_is_PML(ispec)) then
        do j = 1,NGLLZ
          do i = 1,NGLLX
            iglob = ibool(i,j,ispec)
            vector_field_display(1,iglob) = 0.d0
            vector_field_display(2,iglob) = 0.d0
          enddo
        enddo
      endif
    enddo
  endif
!! DK DK quick hack to remove the PMLs from JPEG images if needed

  image_color_data(:,:) = 0.d0

  do k = 1, nb_pixel_loc
    j = ceiling(real(num_pixel_loc(k)) / real(NX_IMAGE_color))
    i = num_pixel_loc(k) - (j-1)*NX_IMAGE_color

    ! avoid edge effects
    if (i < 1 ) i = 1
    if (j < 1 ) j = 1

    if (i > NX_IMAGE_color ) i = NX_IMAGE_color
    if (j > NZ_IMAGE_color ) j = NZ_IMAGE_color

    if (P_SV) then ! P-SH waves, plot a component of vector, its norm, or else pressure
      if (iglob_image_color(i,j) /= -1) then
        if (imagetype_JPEG == 1  .or. imagetype_JPEG == 4 .or. imagetype_JPEG == 7 .or. &
            imagetype_JPEG == 11 .or. imagetype_JPEG == 14) then
          ! draw the X component of the vector
          image_color_data(i,j) = vector_field_display(1,iglob_image_color(i,j))

        else if (imagetype_JPEG == 2 .or. imagetype_JPEG == 5 .or. imagetype_JPEG == 8 .or. &
                imagetype_JPEG == 12 .or. imagetype_JPEG == 15) then
          ! draw the Z component of the vector
          image_color_data(i,j) = vector_field_display(2,iglob_image_color(i,j))

        else if (imagetype_JPEG == 3 .or. imagetype_JPEG == 6 .or. imagetype_JPEG == 9 .or. &
                imagetype_JPEG == 13 .or. imagetype_JPEG == 16) then
          ! draw the norm of the vector
          image_color_data(i,j) = sqrt(vector_field_display(1,iglob_image_color(i,j))**2  &
                                     + vector_field_display(2,iglob_image_color(i,j))**2)

        else if (imagetype_JPEG == 10) then
          ! by convention we have stored pressure in the 2. component of the array
          image_color_data(i,j) = vector_field_display(2,iglob_image_color(i,j))

        else
          call exit_MPI(myrank,'wrong type for JPEG snapshots')
        endif
      endif

    else
      ! SH (membrane) waves, plot y-component
      if (iglob_image_color(i,j) /= -1) image_color_data(i,j) = vector_field_display(1,iglob_image_color(i,j))
    endif
  enddo

  ! assembling array image_color_data on process zero for color output
#ifdef USE_MPI
  if (NPROC > 1) then
    if (myrank == 0) then
      do iproc = 1, NPROC-1
        call MPI_RECV(data_pixel_recv(1),nb_pixel_per_proc(iproc+1), MPI_DOUBLE_PRECISION, &
                      iproc, 43, MPI_COMM_WORLD, MPI_STATUS_IGNORE, ier)

        do k = 1, nb_pixel_per_proc(iproc+1)
          j = ceiling(real(num_pixel_recv(k,iproc+1)) / real(NX_IMAGE_color))
          i = num_pixel_recv(k,iproc+1) - (j-1)*NX_IMAGE_color

          ! avoid edge effects
          if (i < 1) i = 1
          if (j < 1) j = 1

          if (i > NX_IMAGE_color) i = NX_IMAGE_color
          if (j > NZ_IMAGE_color) j = NZ_IMAGE_color

          image_color_data(i,j) = data_pixel_recv(k)
        enddo
      enddo
    else
      do k = 1, nb_pixel_loc
        j = ceiling(real(num_pixel_loc(k)) / real(NX_IMAGE_color))
        i = num_pixel_loc(k) - (j-1)*NX_IMAGE_color

        ! avoid edge effects
        if (i < 1) i = 1
        if (j < 1) j = 1

        if (i > NX_IMAGE_color) i = NX_IMAGE_color
        if (j > NZ_IMAGE_color) j = NZ_IMAGE_color

        if (P_SV) then ! P-SH waves, plot a component of vector, its norm, or else pressure

          if (imagetype_JPEG == 1 .or. imagetype_JPEG == 4 .or. imagetype_JPEG == 7 .or. &
              imagetype_JPEG == 11 .or. imagetype_JPEG == 14) then
             data_pixel_send(k) = vector_field_display(1,iglob_image_color(i,j))  ! draw the X component of the vector

          else if (imagetype_JPEG == 2 .or. imagetype_JPEG == 5 .or. imagetype_JPEG == 8 .or. &
                  imagetype_JPEG == 12 .or. imagetype_JPEG == 15) then
             data_pixel_send(k) = vector_field_display(2,iglob_image_color(i,j))  ! draw the Z component of the vector

          else if (imagetype_JPEG == 3 .or. imagetype_JPEG == 6 .or. imagetype_JPEG == 9 .or. &
                  imagetype_JPEG == 13 .or. imagetype_JPEG == 16) then
            data_pixel_send(k) = sqrt(vector_field_display(1,iglob_image_color(i,j))**2 + &
                                      vector_field_display(2,iglob_image_color(i,j))**2)  ! draw the norm of the vector

          else if (imagetype_JPEG == 10) then
            ! by convention we have stored pressure in the 2. component of the array
            data_pixel_send(k) = vector_field_display(2,iglob_image_color(i,j))

          else
            call exit_MPI(myrank,'wrong type for JPEG snapshots')
          endif

        else ! SH (membrane) waves, plot y-component
          if (iglob_image_color(i,j) /= -1) data_pixel_send(k) = vector_field_display(1,iglob_image_color(i,j))
        endif
      enddo
      call MPI_SEND(data_pixel_send(1),nb_pixel_loc,MPI_DOUBLE_PRECISION, 0, 43, MPI_COMM_WORLD, ier)
    endif
  endif
#else
  ! dummy to avoid compiler warning
  ier = 0
  iproc = NPROC
#endif

  ! creates image
  if (myrank == 0) then
    call create_color_image()

    ! user output
    write(IMAIN,*) 'Color image created'
    call flush_IMAIN()
  endif

  end subroutine write_color_image_snaphot

